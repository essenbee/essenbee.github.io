<!--
    This program is free software: you can redistribute it and/or modify
    it under the terms of the GNU General Public License as published by
    the Free Software Foundation, either version 3 of the License, or
    (at your option) any later version.

    This program is distributed in the hope that it will be useful,
    but WITHOUT ANY WARRANTY; without even the implied warranty of
    MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
    GNU General Public License for more details.

    You should have received a copy of the GNU General Public License
    along with this program.  If not, see <https://www.gnu.org/licenses/>.
-->
<!DOCTYPE html>
<html>
  <head>
    <title>rocket alf</title>
    <meta http-equiv="Content-Type" content="text/html; charset=utf-8">
    <script src="webgl-driver.js" type="text/javascript"></script>
    <link href='https://fonts.googleapis.com/css?family=Josefin Slab' rel='stylesheet'>
    <style>
        body {
            background-color: black;
            color: white;
            font-family: 'Josefin Slab';font-size: 32px;
        }
        canvas.gl {
          position:fixed;
          z-index:-1;
          left:0;
          top:0;
          width:100%;
          height:100%;
        }
    </style>
    <script id="shader-fs" type="x-shader/x-fragment">
      // -----------------------------------------------------------------------
      // BEGIN - Common prelude
      // -----------------------------------------------------------------------
      precision mediump float;

      uniform vec2 iResolution;
      uniform float iTime;
      uniform sampler2D iChannel0;
      uniform sampler2D iChannel1;

      varying highp vec2 vTextureCoord;

      void mainImage(out vec4 fragColor, in vec2 fragCoord);

      void main(void) {
        mainImage(gl_FragColor, vTextureCoord*iResolution);
      }
      // -----------------------------------------------------------------------
      // END - Common prelude
      // -----------------------------------------------------------------------

      float noiseFunction(vec2 pos)
      {
          return texture2D(iChannel0, mod(pos - 0.5, 1.0)).x * 2.0 - 1.0;
      }
      
      float fbm(vec2 pos)
      {
          pos *= 0.02;
          pos.y += iTime * -0.05;
          float sum = 0.0;
          float freq = 1.0;
          float amp = 1.0;
          
          for (int i = 0; i < 4; i++)
          {
              sum += amp * noiseFunction(pos * freq);
              amp *= 0.5;
              freq *= 3.0;
          }
          
          return sum;
      }
      
      vec2 toPolarCoords(vec2 pos)
      {
          float radius = length(pos);
          float angle = atan(pos.y, pos.x);
          return vec2(radius, angle);
      }
      
      vec2 toRectCoords(vec2 polar)
      {
          float x = polar.x * cos(polar.y);
          float y = polar.x * sin(polar.y);
          
          return vec2(x,y);
      }

      float tanh(float x)
      {
        float ex = exp(x);
        float nex = exp(-x);
        float sum = ex + nex;
        return ex/sum - nex/sum;
      }
      
      vec3 fire(vec2 pos)
      {
          pos.y += 0.75;
          pos *= 5.0;
          
          float n = tanh(exp(fbm(pos) - pos.y));
          vec3 col = mix(vec3(pow(n, 0.4)), vec3(1.5 * n, 1.5 * pow(n,3.0), pow(n, 6.0)), n);
          
          return col;
      }
      
      vec4 rocketPlume(vec2 pos)
      {
          pos.y *= -0.75;
          vec2 p = toPolarCoords(pos);
          p.x -= 1.2;
          
          pos = toRectCoords(p);
          vec3 col = fire(pos);
          float alpha = smoothstep(0.1, -0.2, p.x) * (smoothstep(2.5, 2.0, p.y) * smoothstep(0.5, 1.5, p.y));
          
          return vec4(col, alpha);
      }
      
      vec4 alf(vec2 pos)
      {
          pos.y -= 0.06;
          pos.y *= -1.0;
          pos *= 4.0;
          pos += 0.5;
          float a = pos.x >= 0.0 && pos.x <= 1.0 ? 1.0 : 0.0;
          float b = pos.y >= 0.0 && pos.y <= 1.0 ? 1.0 : 0.0;
          vec4 col = texture2D(iChannel1, pos) * a * b;
          return col;
      }
      
      vec3 rocketAlf(vec2 pos)
      {
          vec3 col = vec3(0.0);
          vec4 rocketc = rocketPlume(pos);
          col = mix(col, rocketc.xyz, rocketc.w);
          vec4 alfc = alf(pos);    
          col = mix(col, alfc.xyz, alfc.w);
          return col;
      }

      void mainImage( out vec4 fragColor, in vec2 fragCoord )
{
    // Normalized pixel coordinates (from 0 to 1)
    vec2 uv = (fragCoord/iResolution.xy) * 2.0 - 1.0;
    uv.x *= iResolution.x / iResolution.y;
    float period = 10.0;
    float time = mod(iTime, period);
    float offset = mix(1.0, -3.0, smoothstep(0.0, period, time));
    uv.y += offset;

    vec3 col = rocketAlf(uv * 0.5);    
    // Fade in/out
    col *= smoothstep(0.0, 0.5, time);
    col *= 1.0 - smoothstep(period - 0.5, period, time);
    
    fragColor = vec4(
        col,
        1.0);
}

    </script>

    <script id="shader-vs" type="x-shader/x-vertex">
      attribute highp vec3 aVertexPosition;
      attribute highp vec3 aVertexNormal;
      attribute highp vec2 aTextureCoord;

      varying highp vec2 vTextureCoord;
      varying highp vec3 vNormal;

      void main(void) {
        gl_Position   = vec4(aVertexPosition, 1.0);
        vNormal = aVertexNormal;
        vTextureCoord = aTextureCoord;
      }
    </script>
  </head>

  <body onload="start()">
    <img id="image1" src="RocketAlf2.png" style="visibility: collapse"/>
    <img id="image0" src="noise.png" style="visibility: collapse"/>
    <canvas id="glcanvas" class="gl">
      Your browser doesn't appear to support the HTML5 <code>&lt;canvas&gt;</code> element.
    </canvas>
  </body>
</html>